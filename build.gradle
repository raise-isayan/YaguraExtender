buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'  
    id 'org.asciidoctor.jvm.base' version '4.0.3'
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
    id 'org.asciidoctor.jvm.pdf' version '4.0.3'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

asciidoctor {
    sourceDir = file("src/main/help")
    sources {
      include "help.adoc", "help-ja.adoc"
    }
    attributes = [
      'revnumber': "v${release_version_major}.${release_version_minor}"
    ]
    outputDir = file("help")
}

application {
    mainClass = 'yagura.view.MainFrame'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching ('**/*.properties') {
       expand(project.properties)
       // naitive2ascii
       filter(org.apache.tools.ant.filters.EscapeUnicode)
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

clean.doFirst {
    delete fileTree('release') {
      include '*.jar'
    }
}

shadowJar {
   exclude 'META-INF/*.SF'
   exclude 'META-INF/*.DSA'
   exclude 'META-INF/*.RSA'
   exclude 'junit/**'

   classifier = null
   destinationDirectory = file('release')
   archiveVersion = "v${release_version_major}"
}

task release(type: Zip, dependsOn: ['build', 'asciidoctor']) {
    archiveBaseName ="${rootProject.name}_v${release_version_major}.${release_version_minor}"
    destinationDirectory = file("${projectDir}")
    from rootProject.rootDir
    include '*'
    include 'gradle/**'
    include 'src/**'
    include 'help/**'
    include 'libs/**'
    include 'sample/**'
    include 'release/*.jar'
    exclude 'build'
    exclude 'nbproject'
    exclude '.git'
    exclude '.gradle'
    exclude '*.zip'
}

dependencies {
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    compileOnly 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    compileOnly 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // https://github.com/raise-isayan/BurpExtensionCommons
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // standalone 利用のため
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    implementation 'net.portswigger.burp.extender:burp-extender-api:2.3'
    // https://mvnrepository.com/artifact/net.portswigger.burp.extensions/montoya-api
    implementation 'net.portswigger.burp.extensions:montoya-api:2024.11'
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    implementation 'commons-codec:commons-codec:1.17.1'
    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    implementation 'com.fifesoft:rsyntaxtextarea:3.3.4'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // https://mvnrepository.com/artifact/io.github.rburgst/okhttp-digest
    implementation 'io.github.rburgst:okhttp-digest:3.1.1'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/mockwebserver
    implementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // Unit Test

    // Use JUnit Jupiter for testing.
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

 //   testImplementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://mvnrepository.com/artifact/net.portswigger.burp.extensions/montoya-api
    testImplementation 'net.portswigger.burp.extensions:montoya-api:2024.11'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // https://mvnrepository.com/artifact/io.github.rburgst/okhttp-digest
    testImplementation 'io.github.rburgst:okhttp-digest:3.1.1'
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/mockwebserver
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation 'org.mockito:mockito-core:5.4.0'

    // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk18on
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
